apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: "config.gradle"
apply from: "dependencies.gradle"

def secretkeyPropertiesFile = file("secretkey.properties")
def secretkeyProperties = new Properties()
secretkeyProperties.load(new FileInputStream(secretkeyPropertiesFile))

android {
  compileSdkVersion configs.compileSdkVersion
  buildToolsVersion configs.buildToolsVersion

  defaultConfig {
    applicationId configs.applicationId
    minSdkVersion configs.minSdkVersion
    targetSdkVersion configs.targetSdkVersion
    versionCode configs.versionCode
    versionName configs.versionName

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    release {
      storeFile file('keystore/release.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
  }

  buildTypes {
    debug {
      debuggable true
      minifyEnabled false
      zipAlignEnabled false
      manifestPlaceholders = [enableCrashReporting: "false"]
      buildConfigField "String", "BASE_APP_URL", DEV_BASE_APP_URL
      resValue "string", "APPLE_SECRET_KEY", secretkeyProperties['DEV_APPLE_SECRET_KEY']
    }

    staging {
      debuggable true
      minifyEnabled false
      zipAlignEnabled false
      manifestPlaceholders = [enableCrashReporting: "true"]
      buildConfigField "String", "BASE_APP_URL", STAGE_BASE_APP_URL
      resValue "string", "APPLE_SECRET_KEY", secretkeyProperties['STAGE_APPLE_SECRET_KEY']
    }

    release {
      debuggable false
      minifyEnabled true
      zipAlignEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      manifestPlaceholders = [enableCrashReporting: "true"]
      buildConfigField "String", "BASE_APP_URL", PROD_BASE_APP_URL
      resValue "string", "APPLE_SECRET_KEY", secretkeyProperties['PROD_APPLE_SECRET_KEY']
    }
  }

  sourceSets {
    main {
      java.srcDirs = ['src/main/java']
      res.srcDirs = [
              'src/main/res/layouts/detail',
              'src/main/res/layouts/master',
              'src/main/res/layouts/splash',
              'src/main/res/layouts/common',
              'src/main/res/layouts',

              'src/main/res/drawables/detail',
              'src/main/res/drawables/master',
              'src/main/res/drawables/splash',
              'src/main/res/drawables/common',
              'src/main/res/drawables',

              'src/main/res/layout',
              'src/main/res/drawable',
              'src/main/res'
      ]
    }
  }

  buildFeatures {
    dataBinding true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName = outputFileName.replace("app", "iTunesMovies-v" + defaultConfig.versionName)
    }
  }
}

dependencies {
  implementation fileTree(dir: "libs", include: ["*.jar"])

  // Kotlin
  implementation kotlinDependency
  implementation kotlinXCoroutine

  // AndroidX
  implementation androidXDependencies.appCompat
  implementation androidXDependencies.KTX
  implementation androidXDependencies.cardView
  implementation androidXDependencies.constraintLayout
  implementation androidXDependencies.legacySupport
  implementation androidXDependencies.lifecycleExtension
  implementation androidXDependencies.lifecycleLiveData
  implementation androidXDependencies.lifecycleRuntime
  implementation androidXDependencies.lifecycleViewModel
  implementation androidXDependencies.recyclerView

  // Material
  implementation materialDependency

  // Play Services
  implementation playServicesDependencies.maps
  implementation playServicesDependencies.location

  // Timber
  implementation timberDependency

  // Retrofit
  implementation retrofitDependencies.retrofit
  implementation retrofitDependencies.converterGson
  implementation retrofitCallToLiveDataAdapterDependency

  // OkHttp
  implementation okHttpDependency

  // Room
  implementation roomDependencies.roomRuntime
  implementation roomDependencies.roomKTX
  kapt roomDependencies.roomCompiler

  // Dagger
  implementation daggerDependencies.dagger
  kapt daggerDependencies.compiler

  // Picasso
  implementation picassoDependecy

  // Test
  testImplementation testDependencies.junit
  androidTestImplementation testDependencies.extJunit
  androidTestImplementation testDependencies.espresso
}